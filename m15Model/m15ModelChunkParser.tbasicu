'[] Parsing of m15ModelChunks from string buffer

#INCLUDE Once "m15ModelChunkCollection.tbasicu"
#INCLUDE Once "gBufferBuilder.tbasicu"

Begin Const
  %m15_X = 1, %m15_Y, %m15_Z 
  %m15_PStop 
  %m15_Texture, %m15_U, %m15_V 
  %m15_R, %m15_G, %m15_B 
  %m15_NX, %m15_NY, %m15_NZ
End Const

Type m15ModelChunkParser
  
  Private
 
    Function VertexCount2GBufferType(vertexCount As Long) As Long
      If vertexCount > 4 Then
        Return %TBGL_POLYGON
      Else
        Return Choose(vertexCount, %TBGL_POINTS, %TBGL_LINES, %TBGL_TRIANGLES, %TBGL_QUADS)
      End If
    End Function
  
    Function GetVertexCount(lines() As String, fromIndex As Long) As Long
      Long i
      Long count
      
      For i = fromIndex To UBound(lines)
        Incr count
        If lines(i, %m15_PStop) = "1" Then
          Return count
        End If
      Next
    End Function 
    
    Function IsNextPolygonOfSameType(lines() As String, fromIndex As Long, vertexCount As Long) As Long
      If fromIndex + 1 > CountOf(lines) Then
        Return FALSE
      Else
        Decr vertexCount
        Return lines(fromIndex, %m15_Texture) = lines(fromIndex+1, %m15_Texture) And lines(fromIndex+vertexCount, %m15_PStop) = "1"
      End If
    End Function

    Function BuildPolygon(builder As gBufferBuilder, lines() As String, startIndex As Long, vertexCount As Long)
      Long i, triangle
      
      Long index(3)  = startIndex, startIndex+1, startIndex+2
      
      For triangle = 1 To vertexCount - 2
        For i = 1 To 3
          builder.AddItem()
            builder.SetVertex(lines(index(i), %m15_X), lines(index(i), %m15_Y), lines(index(i), %m15_Z))
            builder.SetColor(lines(index(i), %m15_R), lines(index(i), %m15_G), lines(index(i), %m15_B))
            builder.SetNormal(lines(index(i), %m15_NX), lines(index(i), %m15_NY), lines(index(i), %m15_NZ))
            builder.SetUV(lines(index(i), %m15_U), lines(index(i), %m15_V))      
        Next
        
        index(1) = startIndex, startIndex+1+triangle, startIndex+2+triangle
      Next  
    End Function    
  
  Public
    chunks  As m15ModelChunkCollection
    
    Function Process(content As String) As DWord
      Me.chunks.ClearItems()
      Dim builder As gBufferBuilder()
                 
      String definition = Mid$(content, InStr(content, "POLY"))
      definition = Trim$(Remove$(definition, "POLY,"))
           
      String lines()
      Long   count = Parse(definition, lines, $CRLF, ",")
      
      Long i = 1                     
      Long lastPStop   = lines(i, %m15_PStop)
      Long lastTexture = lines(i, %m15_Texture)
      Long newTexture
      Long polygonVertexCount = Me.GetVertexCount(lines, i)
      
      If Len(lines(count, %m15_X)) = 0 Then Decr count

      While i < count
        Me.BuildPolygon(builder, lines, i, polygonVertexCount)
                
        i += polygonVertexCount
                          
        newTexture = IIf(i < count, lines(i+1, %m15_Texture), lastTexture)        
        If newTexture <> lastTexture Or i >= count Then
          builder.Build(Me.VertexCount2GBufferType(polygonVertexCount))
          Me.chunks.AddItem(builder.GetGBuffer,
                            lastTexture)
                            
          builder.ClearItems()
          lastTexture = newTexture
        End If                                                  
        
        polygonVertexCount = Me.GetVertexCount(lines, i)
      Wend  
      
      Function = Me.chunks.GetCount()
    End Function
End Type
